Type union_superset(Type t1, Type t2) = union_superset([t1, t2]);

Type union_superset([Type] types) = union_type(types);

NeType ne_union_superset([+NeType] types) = ne_union_type(types);

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

Type replace_type_vars_with_type_any(Type type) = replace_type_vars_computed(type, type_any);

// ClsType replace_type_vars_with_type_any(ClsType type) = replace_type_vars_computed(type, type_any);

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

//## WHERE SHOULD THIS GO? IT LOGICALLY BELONG TO THE CODE THAT CALCULATE THE
//## INTERSECTION OF TWO TYPES, BUT APART FROM THE FACT THAT THAT CODE HAS
//## BEEN REMOVED, IT IS ALSO USED BY THE CODE THAT CALCULATE THE INTERSECTION
//## OF A PATTERN AND A TYPE, AND I DON'T WANT THAT CODE TO DEPEND ON THE CODE
//## THAT CALCULATES THE (APPROXIMATE) INTERSECTION OF TWO TYPES

<IntType, empty_type> int_types_intersection(IntType int_type_1, IntType int_type_2) =
  integer,      _             = int_type_2,

  low_ints(),   low_ints()    = low_ints(min(int_type_1.max, int_type_2.max)),
  low_ints(),   high_ints()   = maybe_int_range(int_type_2.min, int_type_1.max),
  low_ints(),   int_range()   = maybe_int_range(int_type_2.min, min(int_type_1.max, max(int_type_2))),

  high_ints(),  high_ints()   = high_ints(max(int_type_1.min, int_type_2.min)),
  high_ints(),  int_range()   = maybe_int_range(max(int_type_1.min, int_type_2.min), max(int_type_2)),

  int_range(),  int_range()   = maybe_int_range(max(int_type_1.min, int_type_2.min), min(max(int_type_1), max(int_type_2))),

  _,            _             = int_types_intersection(int_type_2, int_type_1);


//## THIS SHOULD BE MOVED TO THE utils_2_ctors.h FILE
<IntType, empty_type> maybe_int_range(Int min, Int max) = if max >= min then int_range(min, max) else empty_type;

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit typedefs : [TypeName -> NeType]
{
  [TypeVar] retrieve_type_vars(Type type) =
    TypeVar   = [type],
    _         = retrieve_from_nested_types(type, retrieve_type_vars($, typedefs=typedefs));


  Bool may_match(Pattern ptrn, Type type) = type_pattern_intersection_superset(type, ptrn) != empty_type;


  // The type returned by this function is always a subset of both the
  // input type and the [type implicitly defined by] the input pattern
  Type type_pattern_intersection_superset(Type type, Pattern ptrn) =
    empty_type,         _                 = empty_type,

    _,                  ptrn_any          = type,
    _,                  ptrn_var()        = type_pattern_intersection_superset(type, ptrn.ptrn),
    _,                  ptrn_union(ps?)   = union_type([type_pattern_intersection_superset(type, p) : p <- ps]),

    type_var(),         _                 = type_pattern_intersection_superset(type_any, ptrn),

    type_ref(ts?),      _                 = type_pattern_intersection_superset(expand_type_ref(ts, typedefs), ptrn),

    atom_type,          ptrn_symbol       = atom_type,
    atom_type,          ptrn_symbol(s?)   = symb_type(s),
    atom_type,          _                 = empty_type,

    symb_type(),        ptrn_symbol       = type,
    symb_type(ts?),     ptrn_symbol(ps?)  = if ts == ps then type else empty_type,
    symb_type(),        _                 = empty_type,

    IntType,            ptrn_integer(t?)  = int_types_intersection(type, t),
    IntType,            _                 = empty_type,

    float_type,         _                 = if ptrn == ptrn_float then float_type else empty_type,

    empty_seq_type,     _                 = if ptrn == ptrn_empty_seq then empty_seq_type else empty_type,
    empty_set_type,     _                 = if ptrn == ptrn_empty_set then empty_set_type else empty_type,
    empty_bin_rel_type, _                 = if ptrn == ptrn_empty_bin_rel then empty_bin_rel_type else empty_type,
    empty_tern_rel_type,_                 = if ptrn == ptrn_empty_tern_rel then empty_tern_rel_type else empty_type,

    ne_seq_type(),      ptrn_ne_seq       = type,
    ne_seq_type(),      ptrn_tuple(ps?)   = tuple_type((type_pattern_intersection_superset(type.elem_type, p) : p <- ps)),
    ne_seq_type(),      _                 = empty_type,

    tuple_type(ts?),    ptrn_ne_seq       = type,
    tuple_type(ts?),    ptrn_tuple(ps?)   = if length(ts) == length(ps)
                                            then tuple_type((type_pattern_intersection_superset(t, p) : t, p <- nonempty(zip(ts, ps))))
                                            else empty_type,
    tuple_type(ts?),    _                 = empty_type,

    ne_set_type(),      _                 = if ptrn == ptrn_ne_set then type else empty_type,

    ne_map_type(),      ptrn_ne_bin_rel   = type,
    ne_map_type(),      ptrn_ne_map       = type,
    ne_map_type(),      _                 = empty_type,

    ne_bin_rel_type(),  ptrn_ne_bin_rel   = type,
    ne_bin_rel_type(),  ptrn_ne_map       = ne_map_type(bin_rel_arg_supertype(type, 0), bin_rel_arg_supertype(type, 1)),
    ne_bin_rel_type(),  _                 = empty_type,

    ne_tern_rel_type(), _                 = if ptrn == ptrn_ne_tern_rel then type else empty_type,

    ne_record_type(),   ptrn_ne_map       = type,
    ne_record_type(),   ptrn_ne_bin_rel   = type,
    ne_record_type(),   _                 = empty_type,

    tag_obj_type(),     ptrn_tag_obj()    = { tag_type_int = type_pattern_intersection_superset(type.tag_type, ptrn.tag);
                                              obj_type_int = type_pattern_intersection_superset(type.obj_type, ptrn.obj);
                                              if tag_type_int == empty_type or obj_type_int == empty_type:
                                                return empty_type;
                                              else
                                                fail if not tag_type_int :: TagType; //## BAD BAD BAD. CHEATING. NEEDS SUBSIGNATURES HERE...
                                                return tag_obj_type(tag_type_int, obj_type_int);
                                              ;
                                            },

    tag_obj_type(),     _                 = empty_type,

    union_type(ts?),    _                 = { int_types = [it : t <- ts, it = type_pattern_intersection_superset(t, ptrn), it != empty_type];
                                              return empty_type if int_types == [];
                                              // return only(int_types) if size(int_types) == 1;
                                              // The types here are all union-compatible, since they are all
                                              // subsets of types that were union-compatible to begin with
                                              return union_type(int_types);
                                            };

  ////////////////////////////////////////////////////////////////////////////////
  ////////////////////////////////////////////////////////////////////////////////

  //## THIS IS PRETTY MUCH UNTESTED. IT SHOULD BE TESTED WITH THE TYPE SUBSET TESTING CODE,
  //## AND MAYBE IT WOULD BE GOOD TO DO SOME MANUAL TESTING, ESPECIALLY FOR TUPLE TYPES
  Bool type_contains_obj(Type type, Any obj) =
    empty_type,           _       = false,

    atom_type,            +       = true,
    atom_type,            _       = false,

    symb_type(s?),        _       = obj == bare_obj(s),

    integer,              *       = true,
    integer,              _       = false,

    low_ints(),           *       = obj <= type.max,
    low_ints(),           _       = false,

    high_ints(),          *       = obj >= type.min,
    high_ints(),          _       = false,

    int_range(),          *       = obj >= type.min and obj <= max(type),
    int_range(),          _       = false,

    float_type,           ^       = true,
    float_type,           _       = false,

    type_var(),           _       = {fail;},

    type_ref(ts?),        _       = type_contains_obj(expand_type_ref(ts, typedefs), obj),

    empty_seq_type,       _       = obj == (),
    empty_set_type,       _       = obj == [],
    empty_bin_rel_type,   _       = obj == [:],
    empty_tern_rel_type,  _       = obj == [::],

    ne_seq_type(),        ()      = obj != () and all((type_contains_obj(type.elem_type, e) : e <- obj)),
    ne_seq_type(),        _       = false,

    ne_set_type(),        []      = obj != [] and not (e <- obj : not type_contains_obj(type.elem_type, e)),
    ne_set_type(),        _       = false,

    ne_map_type(),        [->]    = obj != [:] and
                                    not (
                                      k, v <- obj :
                                        not type_contains_obj(type.key_type, k) or
                                        not type_contains_obj(type.value_type, v)
                                    ),
    ne_map_type(),        _       = false,

    ne_bin_rel_type(),    [:]     = obj != [:] and
                                    not (
                                      l, r <- obj :
                                        not (s <- type.signatures :
                                          type_contains_obj(s(0), l) and type_contains_obj(s(1), r)
                                        )
                                    ),
    ne_bin_rel_type(),    _       = false,

    ne_tern_rel_type(),   [::]    = obj != [::] and
                                    not (
                                      l, m, r <- obj :
                                        not (s <- type.signatures :
                                          all((type_contains_obj(s(i), v) : v @ i <- (l, m, r)))
                                        )
                                    ),
    ne_tern_rel_type(),   _       = false,

    ne_record_type(fs?),  [->]    = obj != [:] and subset([l : l, f <- fs, not f.optional], keys(obj)) and
                                    not (k, v <- obj : not has_key(fs, k) or not type_contains_obj(fs[k].type, v)),
    ne_record_type(fs?),  _       = false,

    tuple_type(ts?),      ()      = length(ts) == length(obj) and all((type_contains_obj(t, e) : t, e <- zip(ts, obj))),
    tuple_type(ts?),      _       = false,

    tag_obj_type(),       t?(o?)  = type_contains_obj(type.tag_type, t) and type_contains_obj(type.obj_type, o),
    tag_obj_type(),       _       = false,

    union_type(ts?),      _       = (t <- ts : type_contains_obj(t, obj));

  ////////////////////////////////////////////////////////////////////////////////
  ////////////////////////////////////////////////////////////////////////////////

  //## I COULD ASSERT THAT THE RESULTING TYPE IS A SUBSET OF THE ORIGINAL ONE
  //## THAT WOULD INTRODUCE A CYCLIC DEPENDENCY (ALBEIT ONLY IN DEBUG MODE) BETWEEN
  //## TYPE UTILS AND SUBTYPE CHECKING. ALSO THE SYNTAX WOULD NOT BE GREAT.
  Type subtract_obj_from_type(Type type, Any obj) =
    symb_type(object(s?))   = if obj == s then empty_type else type,

    low_ints()              = if obj == type.max then low_ints(type.max-1) else type,
    high_ints()             = if obj == type.min then high_ints(type.min+1) else type,
    int_range()             = { new_min = type.min + bit(obj == type.min);
                                new_max = max(type) - bit(obj == max(type));
                                return if new_min <= new_max then int_range(new_min, new_max) else empty_type;
                              },

    type_ref(ts?)           = { exp_type = expand_type_ref(ts, typedefs);
                                res_type = subtract_obj_from_type(exp_type, obj);
                                return if res_type != exp_type then res_type else type;
                              },

    empty_seq_type          = if obj == () then empty_type else type,
    empty_set_type          = if obj == [] then empty_type else type,
    empty_bin_rel_type      = if obj == [:] then empty_type else type,

    union_type(ts?)         = { nts = [nt : t <- ts, nt = subtract_obj_from_type(t, obj), nt != empty_type];
                                return if nts == [] then empty_type else union_type(nts);
                              },

    _                       = type;
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

FnType signature(Function fn) =
  fn_type(
    args:       (a.type : a <- fn.args),
    impl_args:  fn.impl_args,
    ret_type:   fn.res_type,
    constrs:    fn.constrs
  );

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

BuiltInType builtin_signature(BuiltIn) =
  neg             = unary_int_op,
  add             = binary_int_op,
  sub             = binary_int_op,
  mult            = binary_int_op,
  div             = binary_int_op,
  mod             = binary_int_op,

  lt              = binary_int_pred,
  le              = binary_int_pred,
  gt              = binary_int_pred,
  ge              = binary_int_pred,

  str             = builtin_type((atom_type),                          type_string),
  symb            = builtin_type((type_string),                        atom_type),
  len             = builtin_type((type_seq),                           high_ints(0)),
  size            = builtin_type((type_set_rel),                       high_ints(0)),
  slice           = builtin_type((type_seq_t, integer, integer),       type_seq_t),
  cat             = builtin_type((type_seq_t, type_seq_t),             type_seq_t),
  rev             = builtin_type((type_seq_t),                         type_seq_t),
  isort           = builtin_type((type_set_t),                         type_seq_t),

  tag             = builtin_type((type_tagged_obj),                    atom_type), // The return type is too loose, and it is ignore in the code
  obj             = builtin_type((type_tagged_obj),                    type_any),  // Ditto

  parse_val       = builtin_type((type_string),                        type_result(type_any, high_ints(0))),

  fneg            = unary_float_op,
  fadd            = binary_float_op,
  fsub            = binary_float_op,
  fmult           = binary_float_op,
  fdiv            = binary_float_op,
  fsqrt           = unary_float_op,
  // floor           = ,
  // ceiling         = ,
  float           = builtin_type((integer),                            float_type),
  mantissa        = builtin_type((float_type),                         integer),
  dec_exp         = builtin_type((float_type),                         integer),

  rand_nat        = builtin_type((type_nz_nat),                        type_nat),
  rand_elem       = builtin_type((type_set_t),                         type_var(:t)), //## THE TYPE OF THE ARGUMENT SHOULD BE NONEMPTY SET
  counter         = builtin_type((type_any),                           type_nat),

  ticks           = builtin_type((type_any),                           integer);

BuiltInType unary_int_op    = builtin_type((integer), integer);
BuiltInType binary_int_op   = builtin_type((integer, integer), integer);
BuiltInType binary_int_pred = builtin_type((integer, integer), type_bool);

BuiltInType unary_float_op  = builtin_type((float_type), float_type);
BuiltInType binary_float_op = builtin_type((float_type, float_type), float_type);

NeType type_seq_t             = type_seq(type_var(:t));
NeType type_set_t             = type_set(type_var(:t));
NeType type_map_t1_t2         = type_map(type_var(:t1), type_var(:t2));

// Type type_bare_list(Type elem_type) = type_ref(par_type_symbol(builtin_type_symbol(:bare_list), (elem_type)));

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

NeType type_true  = symb_type(true);
NeType type_false = symb_type(false);

NeType type_bool = ne_union_type([type_true, type_false]);

NeType type_nat = high_ints(0);
NeType type_nz_nat = high_ints(1);

NeType type_seq = type_seq(type_any);
NeType type_set = type_set(type_any);
NeType type_map = type_map(type_any, type_any);
NeType type_bin_rel = type_bin_rel([(type_any, type_any)], false);
NeType type_tern_rel = type_tern_rel([(type_any, type_any, type_any)], false);

NeType type_set_rel = ne_union_type([type_set, type_bin_rel, type_tern_rel]);

NeType type_unary_pseudocall_target = ne_union_type([atom_type, type_seq, type_set, type_map]);

NeType type_ne_seq      = ne_seq_type(type_any);
NeType type_ne_set      = ne_set_type(type_any);
NeType type_ne_map      = ne_map_type(type_any, type_any);
NeType type_ne_bin_rel  = ne_bin_rel_type_nonvoid([(type_any, type_any)]);
NeType type_ne_tern_rel = ne_tern_rel_type_nonvoid([(type_any, type_any, type_any)]);

TagObjType type_tagged_obj = tag_obj_type(atom_type, type_any);

NeType type_seq(NeType elem_type, Bool nonempty) = if nonempty then ne_seq_type(elem_type) else type_seq(elem_type);
NeType type_set(NeType elem_type, Bool nonempty) = if nonempty then ne_set_type(elem_type) else type_set(elem_type);

Type type_seq(<empty_type>, Bool nonempty) = if nonempty then empty_type else empty_seq_type;
Type type_set(<empty_type>, Bool nonempty) = if nonempty then empty_type else empty_set_type;

NeType type_map(NeType key_type, NeType value_type, Bool nonempty) =
  if nonempty then ne_map_type(key_type, value_type) else type_map(key_type, value_type);

NeType type_seq(NeType elem_type)  = ne_union_type([empty_seq_type, ne_seq_type(elem_type)]);
NeType type_set(NeType elem_type)  = ne_union_type([empty_set_type, ne_set_type(elem_type)]);
NeType type_mset(NeType elem_type) = ne_union_type([empty_bin_rel_type, ne_map_type(elem_type, high_ints(1))]);

NeType type_tuple(<2..*> size)  = ne_tuple_type(nonempty(size * (type_any)));

NeType type_map(NeType key_type, NeType value_type) =
  ne_union_type([empty_bin_rel_type, ne_map_type(key_type, value_type)]);

NeType type_bin_rel([+(NeType, NeType)] signatures, Bool nonempty) {
  type = ne_bin_rel_type_nonvoid(signatures);
  type = ne_union_type([empty_bin_rel_type, type]) if not nonempty;
  return type;
}

NeType type_tern_rel([+(NeType, NeType, NeType)] signatures, Bool nonempty) {
  type = ne_tern_rel_type_nonvoid(signatures);
  type = ne_union_type([empty_tern_rel_type, type]) if not nonempty;
  return type;
}

NeType type_string = tag_obj_type(symb_type(:string), ne_union_type([empty_seq_type, ne_seq_type(type_nat)]));

NeType type_any = type_ref(builtin_type_symbol(:any));

NeType type_dot_access_target =
  ne_union_type([
    type_map(atom_type, type_any),
    tag_obj_type(atom_type, type_map(atom_type, type_any))
  ]);

NeType type_result(NeType res_type, NeType err_type) =
  ne_union_type([
    tag_obj_type(symb_type(:success), res_type),
    tag_obj_type(symb_type(:failure), err_type)
  ]);