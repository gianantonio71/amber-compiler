type Trie[T1, T2] = [T1 -> (Maybe[T2], Trie[T1, T2])];


Trie[T1, T2] trie([T1+ -> T2] map)
{
  keys = [ks(0) : ks, v <- map];
  terminals = [k -> if map((k)) then just(map[(k)]) else nothing : k <- keys];
  continuations = [[ks(0) -> [nonempty(right_subseq(ks, 1)) -> v]] : ks, v <- map, |ks| > 1];
  merged_continuations = [k -> merge(submaps) : k, submaps <- merge_values(continuations)];
  trie = [k -> (t, trie(lookup(merged_continuations, k, []))) : k, t <- terminals];
  return trie;
}


(Maybe[T2], Nat) trie_lookup(Trie[T1, T2] trie, T1+ seq)
{
  head = head(seq);
  tail = tail(seq);
  return (nothing, 0) if not trie(head);
  maybe_terminal, subtrie = trie[head];
  return (maybe_terminal, 1) if tail == ();
  subres, consumed = trie_lookup(subtrie, tail);
  return if subres != nothing then (subres, nat(consumed+1)) else (maybe_terminal, 1);
}

