
//## FIND BETTER NAME
type FinalMatchAction = set_found_var_and_leave(var: ObjVar),
                        eval_expr_and_add_to_set(expr: Expr, stream_var: StreamVar),
                        eval_exprs_and_add_to_map(
                          key_expr:         Expr,
                          value_expr:       Expr,
                          key_stream_var:   StreamVar,
                          value_stream_var: StreamVar
                        );

//## FIND BETTER NAME
type CondMatchAction  = cond_match_action(cond: Expr, action: FinalMatchAction);

type MatchAction      = FinalMatchAction,
                        CondMatchAction,
                        match_action(clause: Clause, action: MatchAction);



FinalMatchAction set_found_var_and_leave(ObjVar v) = set_found_var_and_leave(var: v);

FinalMatchAction eval_expr_and_add_to_set(Expr e, StreamVar sv) =
  eval_expr_and_add_to_set(expr: e, stream_var: sv);

FinalMatchAction eval_exprs_and_add_to_map(Expr ke, Expr ve, StreamVar ksv, StreamVar vsv) =
  eval_exprs_and_add_to_map(
    key_expr:         ke,
    value_expr:       ve,
    key_stream_var:   ksv,
    value_stream_var: vsv
  );

CondMatchAction action(Expr c, FinalMatchAction a) = cond_match_action(cond: c, action: a);

MatchAction action(Clause c, MatchAction a) = match_action(clause: c, action: a);


using
{
  Nat next_obj_var_id,
  Nat next_int_var_id,
  Nat next_bool_var_id,
  Nat next_set_it_var_id,
  Nat next_seq_it_var_id,
  Nat next_map_it_var_id,
  Nat next_vector_var_id,
  Nat next_stream_var_id;


  [Instr] gen_code(MatchAction action, Var* loc_bound_vars):
    match_action()              = gen_iter_code(action.clause, loc_bound_vars, action.action),
    cond_match_action()         = gen_code_for_cond_match_action(action.cond, action.action, loc_bound_vars),
    set_found_var_and_leave()   = [set_var(action.var, obj_true), exit_block],
    eval_expr_and_add_to_set()  = gen_code_for_eval_expr_and_add_to_set(action.expr, action.stream_var),
    eval_exprs_and_add_to_map() = gen_code_for_eval_exprs_and_add_to_map(action.key_expr, action.value_expr, action.key_stream_var, action.value_stream_var);


  [Instr] gen_code_for_cond_match_action(Expr cond, FinalMatchAction action, Var* loc_bound_vars)
  {
    cond_var    = lvar(next_obj_var_id);
    cond_info   = gen_eval_info(cond, cond_var, next_obj_var_id = nat(next_obj_var_id + 1));
    action_code = gen_code(action, loc_bound_vars);

    return cond_info.eval_code &
           [ check(is_bool(cond_info.expr)),
             do_if(is_true(cond_info.expr), action_code)
           ];
  }


  [Instr] gen_code_for_eval_expr_and_add_to_set(Expr expr, StreamVar stream_var)
  {
    tmp_var = lvar(next_obj_var_id);
    info    = gen_eval_info(expr, tmp_var, next_obj_var_id = nat(next_obj_var_id + 1));
    return info.add_ref_eval_code & [append(stream_var, info.expr)];
  }


  [Instr] gen_code_for_eval_exprs_and_add_to_map(Expr key_expr, Expr value_expr, StreamVar key_stream_var, StreamVar value_stream_var)
  {
    tmp_var    = lvar(next_obj_var_id);

    key_info   = gen_eval_info(key_expr, tmp_var, next_obj_var_id = nat(next_obj_var_id + 1));
    value_info = gen_eval_info(value_expr, tmp_var, next_obj_var_id = nat(next_obj_var_id + 1));

    return key_info.add_ref_eval_code   & [append(key_stream_var, key_info.expr)] &
           value_info.add_ref_eval_code & [append(value_stream_var, value_info.expr)];
  }


  [Instr^] gen_iter_code(Clause clause, Var* loc_bound_vars, MatchAction action):
    in_clause()       = gen_iter_code_for_in_clause(clause.ptrn, clause.src, loc_bound_vars, action),
    map_in_clause()   = gen_iter_code_for_map_in_clause(clause.key_ptrn, clause.value_ptrn, clause.src, loc_bound_vars, action),
    or_clause()       = nonempty(gen_iter_code(clause.left, loc_bound_vars, action) & gen_iter_code(clause.right, loc_bound_vars, action)),
    and_clause()      = gen_iter_code(clause.left, loc_bound_vars, action(clause.right, action));


  [Instr^] gen_iter_code_for_in_clause(Pattern ptrn, Expr src, Var* loc_bound_vars, MatchAction action)
  {
    src_var  = lvar(next_obj_var_id);
    tmp_var  = lvar(nat(next_obj_var_id + 1));
    res_var  = bvar(next_bool_var_id);
    it_var   = set_it_var(next_set_it_var_id);

    let (next_obj_var_id = nat(next_obj_var_id + 1))
      // Variables to avoid: src_var
      src_info = gen_eval_info(src, src_var);

      let (next_set_it_var_id = nat(next_set_it_var_id + 1))
        // Variables to avoid: src_var, it_var
        next_step_code = gen_code(action, loc_bound_vars & new_vars(ptrn));

        let (next_obj_var_id = nat(next_obj_var_id + 1), next_bool_var_id = nat(next_bool_var_id + 1))
          // Variables to avoid: src_var, it_var, res_var, tmp_var
          match_code = gen_ptrn_matching_code(ptrn, tmp_var, res_var, loc_bound_vars);
        ;
      ;
    ;

    loop_code = [
      get_iter(it_var, src_info.expr),
      repeat(
        nonempty(
          [ break_if(is_out_of_range(it_var)),
            set_var(tmp_var, get_curr_obj(it_var))
          ] &
          match_code &
          [ do_if(res_var, next_step_code),
            move_forward(it_var)
          ]
        )
      )
    ];

    return nonempty(src_info.eval_code & loop_code & src_info.cleanup_code);
  }


  [Instr^] gen_iter_code_for_map_in_clause(Pattern key_ptrn, Pattern value_ptrn, Expr src, Var* loc_bound_vars, MatchAction action)
  {
    src_var  = lvar(next_obj_var_id);
    tmp_var  = lvar(nat(next_obj_var_id + 1));
    res_var  = bvar(next_bool_var_id);
    it_var   = map_it_var(next_map_it_var_id);

    let (next_obj_var_id = nat(next_obj_var_id + 1))
      // Variables to avoid: src_var
      src_info = gen_eval_info(src, src_var);

      let (next_map_it_var_id = nat(next_map_it_var_id + 1))
        // Variables to avoid: src_var, it_var
        next_step_code = gen_code(action, loc_bound_vars & new_vars(key_ptrn) & new_vars(value_ptrn));

        let (next_obj_var_id = nat(next_obj_var_id + 1), next_bool_var_id = nat(next_bool_var_id + 1))
          // Variables to avoid: src_var, it_var, tmp_var, res_var
          key_ptrn_code = gen_ptrn_matching_code(key_ptrn, tmp_var, res_var, loc_bound_vars);

          // Variables to avoid: src_var, it_var, tmp_var, res_var
          value_ptrn_code = gen_ptrn_matching_code(value_ptrn, tmp_var, res_var, loc_bound_vars);
        ;
      ;
    ;

    loop_code = [
      get_iter(it_var, src_info.expr),
      repeat(
        nonempty(
          [ break_if(is_out_of_range(it_var)),
           set_var(tmp_var, get_curr_key(it_var))
          ] & key_ptrn_code &
          [ do_if(
             res_var,
             [set_var(tmp_var, get_curr_value(it_var))] &
             value_ptrn_code &
             [do_if(res_var,next_step_code)]
           ),
           move_forward(it_var)
          ]
        )
      )
    ];

    return nonempty(src_info.eval_code & loop_code & src_info.cleanup_code);
  }
}
